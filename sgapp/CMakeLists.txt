cmake_minimum_required(VERSION 3.19)

project(sgapp LANGUAGES CXX)

file(GLOB_RECURSE
  SOURCE_FILES
    ${PROJECT_SOURCE_DIR}/*.cpp
    ${PROJECT_SOURCE_DIR}/*.h)
    
file(GLOB_RECURSE
  RESOURCE_FILES
  RELATIVE ${PROJECT_SOURCE_DIR}
  CONFIGURE_DEPENDS
    ${PROJECT_SOURCE_DIR}/assets/**)

FOREACH(RESOURCE_FILE ${RESOURCE_FILES})
  add_custom_command(
    OUTPUT ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/${RESOURCE_FILE}
    COMMAND ${CMAKE_COMMAND} -E copy
      ${PROJECT_SOURCE_DIR}/${RESOURCE_FILE}
      ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/${RESOURCE_FILE}
    DEPENDS ${PROJECT_SOURCE_DIR}/${RESOURCE_FILE}
  )
ENDFOREACH()

list(TRANSFORM RESOURCE_FILES PREPEND ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/)

source_group(TREE ${CMAKE_RUNTIME_OUTPUT_DIRECTORY} FILES ${RESOURCE_FILES})
source_group(TREE ${PROJECT_SOURCE_DIR} FILES ${SOURCE_FILES})

add_executable(${PROJECT_NAME}
  ${RESOURCE_FILES}
  
  ${SOURCE_FILES}
)

include(FetchContent)

FetchContent_Declare(
  SDL3
  URL https://github.com/libsdl-org/SDL/releases/download/release-3.2.18/SDL3-3.2.18.tar.gz
  DOWNLOAD_EXTRACT_TIMESTAMP ON
)

set(SDL_SHARED OFF)
set(SDL_STATIC ON)
set(SDL_TEST OFF)

FetchContent_MakeAvailable(SDL3)

set_target_properties(${PROJECT_NAME} PROPERTIES CXX_STANDARD 20)
target_link_libraries(${PROJECT_NAME} PRIVATE
	scenegraph
	SDL3::SDL3-static
)
